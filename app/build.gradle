//Load API Key information in from property file
Properties props = new Properties()
try {
    props.load(file('secret.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing secret.properties, check the secret.properties.sample file.");
}

apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    defaultConfig {
        applicationId "net.yusukezzz.ssmtc"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
        buildConfigField "String", "OPENGRAPH_API_BASE_URL", "\"${props.getProperty("ogApiBaseUrl")}\""
    }
    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    androidExtensions {
        experimental = true
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    ext {
        kotlin_coroutines_version = '0.20'
        support_version = '27.0.2'
        kovenant_version = '3.3.0'
        retrofit_version = '2.3.0'
        permission_dispatcher_version = '3.0.1'
        dagger_version = '2.11'
        leakcanary_version = '1.5.4'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.github.hotchemi:permissionsdispatcher:$permission_dispatcher_version"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permission_dispatcher_version"
    implementation "nl.komponents.kovenant:kovenant:$kovenant_version"
    implementation "nl.komponents.kovenant:kovenant-combine:$kovenant_version"
    implementation "nl.komponents.kovenant:kovenant-android:$kovenant_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    testImplementation('org.threeten:threetenbp:latest.release') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    implementation 'se.akerfeldt:okhttp-signpost:1.1.0'
    implementation 'id.zelory:compressor:2.1.0'

    // html entity decoding
    implementation 'org.apache.commons:commons-text:1.1'
    implementation 'saschpe.android:customtabs:1.0.8'
    implementation 'com.deploygate:sdk:4.0.0'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:latest.release'
    testImplementation 'org.robolectric:robolectric:3.5.1'
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

// ignore unstable releases
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'eap'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
task depupd(dependsOn: dependencyUpdates)

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def time = "\nTest time: ${(result.endTime - result.startTime) / 1000} secs\n"
                def output = "${time}\nResults: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)\n"
                println(output)
            }
        }
    }
}
