//Load API Key information in from property file
Properties props = new Properties()
try {
    props.load(file('secret.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing secret.properties, check the secret.properties.sample file.");
}

apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "net.yusukezzz.ssmtc"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
    }
    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    ext {
        support_version = '25.3.1'
        kovenant_version = '3.3.0'
        retrofit_version = '2.2.0'
        paperparcel_version = '2.0.1'
        permission_dispatcher_version = '2.3.2'
        dagger_version = '2.10'
        leakcanary_version = '1.5'
    }

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile "com.android.support:design:$support_version"

    compile "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    compile "com.github.hotchemi:permissionsdispatcher:$permission_dispatcher_version"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permission_dispatcher_version"

    compile "nl.komponents.kovenant:kovenant:$kovenant_version"
    compile "nl.komponents.kovenant:kovenant-combine:$kovenant_version"
    compile "nl.komponents.kovenant:kovenant-android:$kovenant_version"

    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    testCompile('org.threeten:threetenbp:1.3.3') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    compile 'se.akerfeldt:okhttp-signpost:1.1.0'
    compile 'id.zelory:compressor:1.0.5'

    compile 'com.github.gfx.util.encrypt:android-encrypt-utils:2.0.0'

    // html entity decoding
    compile 'org.apache.commons:commons-lang3:3.5'

    compile "nz.bradcampbell:paperparcel:$paperparcel_version"
    compile "nz.bradcampbell:paperparcel-kotlin:$paperparcel_version"
    kapt "nz.bradcampbell:paperparcel-compiler:$paperparcel_version"

    compile 'com.deploygate:sdk:3.1.1'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.7.21"
    testCompile "org.robolectric:robolectric:3.3.2"
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

// ignore unstable releases
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'eap'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
task depupd(dependsOn: dependencyUpdates)

tasks.withType(Test) {
    testLogging {
        afterTest { desc, result ->
            println(desc.className + " > " + desc.name + " ${result.resultType}")
            if (result.resultType == TestResult.ResultType.FAILURE && result.exception != null) {
                result.exception.printStackTrace()
            }
        }

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def time = "\nTest time: ${(result.endTime - result.startTime) / 1000} secs\n"
                def output = "${time}\nResults: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)\n"
                println(output)
            }
        }
    }
}
