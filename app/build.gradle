//Load API Key information in from property file
Properties props = new Properties()
try {
    props.load(file('secret.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing secret.properties, check the secret.properties.sample file.");
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "net.yusukezzz.ssmtc"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    ext {
        support_version = '25.0.0'
        kovenant_version = '3.3.0'
        retrofit_version = '2.1.0'
        paperparcel_version = '1.0.0'
        permission_dispatcher_version = '2.2.0'
    }

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:customtabs:$support_version"
    compile "com.android.support:design:$support_version"

    compile "com.github.hotchemi:permissionsdispatcher:$permission_dispatcher_version"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permission_dispatcher_version"

    compile "nl.komponents.kovenant:kovenant:$kovenant_version"
    compile "nl.komponents.kovenant:kovenant-android:$kovenant_version"

    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'net.danlew:android.joda:2.9.4.2'
    compile 'se.akerfeldt:okhttp-signpost:1.1.0'
    compile 'id.zelory:compressor:1.0.3'

    compile 'com.github.gfx.util.encrypt:android-encrypt-utils:2.0.0'

    // photo gallery
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.slider:library:1.1.5@aar'

    // html entity decoding
    compile 'org.apache.commons:commons-lang3:3.4'

    compile "com.github.grandstaish.paperparcel:paperparcel:$paperparcel_version"
    compile "com.github.grandstaish.paperparcel:paperparcel-kotlin:$paperparcel_version"
    kapt "com.github.grandstaish.paperparcel:compiler:$paperparcel_version"

    compile 'com.facebook.stetho:stetho:1.4.1'
    testCompile 'junit:junit:4.12'
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.cookpad.android.licensetools:license-tools-plugin:0.17.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

task depchk(dependsOn: dependencyUpdates)
